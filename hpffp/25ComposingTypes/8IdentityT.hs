{-# LANGUAGE InstanceSigs #-}

import Control.Monad (join)

-- Just as Identity helps show off the most basic essence of Functor,
-- Applicative, and Monad, IdentityT is going to help you begin to
-- understand monad transformers

-- Using this type that doesnâ€™t have a
-- lot of interesting stuff going on with it will help keep us focused on
-- the types and the important fundamentals of transformers.

-- What we see here will be applicable to other transformers as well, but types like
-- Maybe and list introduce other possibilities (failure cases, empty lists)
-- that complicate things a bit.

-- First, letâ€™s compare the Identity type youâ€™ve seen up to this point and
-- our new IdentityT datatype:

-- Plain old Identity. 'a' can be something with
-- more structure, but it's not required and
-- Identity won't know anything about it.
newtype Identity a = Identity {runIdentity :: a}
    deriving (Eq, Show)

-- The identity monad transformer, serving only to
-- to specify that additional structure should exist.
newtype IdentityT f a = IdentityT {runIdentityT :: f a}
    deriving (Eq, Show)

-- What changed here is that we added an extra type argument.

-- Then we want Functor instances for both Identity and IdentityT:

instance Functor Identity where
    fmap f (Identity a) = Identity (f a)

instance (Functor m) => Functor (IdentityT m) where
    fmap f (IdentityT fa) = IdentityT (fmap f fa)

-- The IdentityT instance here should look similar to the Functor instance
-- for the One datatype above â€” the ğ‘“ ğ‘ argument is the value inside the
-- IdentityT with the (untouchable) structure wrapped around it.

-- All we know about that additional layer of structure wrapped around
-- the ğ‘ value is that it is a Functor

-- We also want Applicative instances for each:

instance Applicative Identity where
    pure = Identity
    (Identity f) <*> (Identity a) = Identity (f a)

instance (Applicative m) => Applicative (IdentityT m) where
    pure = IdentityT . pure
    (IdentityT mf) <*> (IdentityT mx) = IdentityT (mf <*> mx)

-- The Identity instance should be familiar. In the IdentityT instance,
-- the ğ‘“ ğ‘ğ‘ variable represents the f (a -> b) that is the first argument
-- of (<*>). Since this can rely on the Applicative instance for ğ‘š to
-- handle that bit, this instance defines how to applicatively apply in the
-- presence of that outer IdentityT layer.

-- Finally, we want some Monad instances:

instance Monad Identity where
    (>>=) :: Identity a -> (a -> Identity b) -> Identity b
    (Identity a) >>= fn = fn a

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    -- [1]         [2][3]    [8]      [4][5]    [7]         [6]
    (IdentityT ma) >>= f = IdentityT $ ma >>= runIdentityT . f
-}

-- The Monad instance is tricky, so weâ€™re going to do a few things to
-- break it down. Keep in mind that Monad is where we have to really
-- use concrete type information from IdentityT in order to make the
-- types fit.

-- ? The bind breakdown
-- ? Implement the bind, step by step

-- Now weâ€™re going to backtrack and go through implementing that bind
-- step by step

-- The goal here is to demystify what weâ€™ve done and enable
-- you to write your own instances for whatever monad transformer you
-- might need to implement yourself.

-- Weâ€™ll go ahead and start back at the beginning, but with InstanceSigs
-- turned on so we can see the type:

-- !{-#LANGUAGE InstanceSigs #-}

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f = undefined
-}

-- Letâ€™s leave the undefined as our final return expression, then use let
-- bindings and contradiction to see the types of our attempts at making
-- a Monad instance. Weâ€™re going to use the bottom value (undefined)
-- to defer the parts of the proof weâ€™re obligated to produce until weâ€™re
-- ready. First, letâ€™s just get a let binding in place and see it load,
-- even if the code doesnâ€™t actually work

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb = ma >>= f
         in undefined
-}

-- Weâ€™re using ğ‘ğ‘–ğ‘šğ‘ as a mnemonic for the parts of the whole thing that
-- weâ€™re trying to implement.

-- Here we get an error:

-- ! Couldn't match type â€˜mâ€™ with â€˜IdentityT mâ€™

-- That type error isnâ€™t the most helpful thing in the world. Itâ€™s hard to
-- know whatâ€™s wrong from that. So, weâ€™ll poke at this a bit in order to
-- get a more helpful type error.

-- First, weâ€™ll do something we know should work. Weâ€™ll use fmap instead.

-- Because that will typecheck (but not give us the same result as (>>=)),
-- we need to do something to give the compiler a chance to contradict
-- us and tell us the real type

-- We force that type error by asserting a fully polymorphic type for ğ‘ğ‘–ğ‘šğ‘:

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb :: a
            aimb = fmap f ma
         in undefined
-}

-- The type we just asserted for ğ‘ğ‘–ğ‘šğ‘ is impossible; weâ€™ve just said it could
-- be every type, and it canâ€™t. The only thing that can have that type is
-- bottom, as bottom inhabits all types.

-- Conveniently, GHC will let us know what ğ‘ğ‘–ğ‘šğ‘ actually is:

-- ! Couldn't match expected type â€˜a1â€™
-- ! with actual type â€˜m (IdentityT m b)â€™

-- With the current implementation, ğ‘ğ‘–ğ‘šğ‘ has the type m (IdentityT
-- m b). Now we can see the real problem: there is an IdentityT layer
-- in between the two bits of ğ‘š that we need to join in order to have a
-- monad.

-- ? Hereâ€™s a breakdown:

-- (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
-- the pattern match on IdentityT is
-- basically our having lifted over it
-- Problem is, we >>='d

-- * (a -> IdentityT m b)

-- over

-- * m a

-- and got

-- * m (IdentityT m b)

-- It doesnâ€™t typecheck because (>>=) merges structure of the same type
-- after lifting (remember: itâ€™s fmap composed with join under the hood).

-- Had our type been m (m b) after binding f over ma it wouldâ€™ve worked
-- fine. As it is, we need to find a way to get the two bits of ğ‘š together
-- without an intervening IdentityT layer.

-- Weâ€™re going to continue with having separate fmap and join instead of
-- using (>>=) because it makes the step-wise manipulation of structure
-- easier to see

-- How do we get rid of the IdentityT in the middle of the
-- two ğ‘š structures?

-- Well, we know ğ‘š is a Monad, which means itâ€™s also
-- a Functor. So, we can use runIdentityT to get rid of the IdentityT
-- structure in the middle of the stack of types:

-- * Trying to change m (IdentityT m b)

-- * into m (m b)

-- Note:
-- ? runIdentityT :: IdentityT f a -> f a
-- ? fmap runIdentityT :: Functor f => f (IdentityT f1 a) -> f (f1 a)

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb :: a
            aimb = fmap runIdentityT (fmap f ma)
         in undefined
-}

-- And when we load this code, we get an encouraging type error:

-- ! Couldn't match expected type â€˜a1â€™
-- ! with actual type â€˜m (m b)â€™

-- Itâ€™s telling us we have achieved the type m (m b), so now we know
-- how to get where we want

-- The ğ‘1 here is the ğ‘ we had assigned to ğ‘ğ‘–ğ‘šğ‘,
-- but itâ€™s telling us that our actual type is not what we asserted but this
-- other type.

-- Thus we have discovered what our actual type is, which
-- gives us a clue about how to fix it

-- Weâ€™ll use join from Control.Monad to merge the nested ğ‘š structure:

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb :: a
            aimb = join (fmap runIdentityT (fmap f ma))
         in undefined
-}

-- And when we load it, the compiler tells us we finally have an m b which
-- we can return:

-- ! Couldn't match expected type â€˜a1â€™
-- ! with actual type â€˜m bâ€™

-- In fact, before we begin cleaning up our code, we can verify this is the
-- case real quick:

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb = join (fmap runIdentityT (fmap f ma))
         in aimb
-}

-- We removed the type declaration for aimb and also changed the in
-- undefined. But we know that ğ‘ğ‘–ğ‘šğ‘ has the actual type m b, so this
-- wonâ€™t work. Why? If we take a look at the type error:

-- ! Couldn't match type â€˜mâ€™ with â€˜IdentityT mâ€™

-- The (>>=) we are implementing has a final result of type IdentityT
-- m b, so the type of ğ‘ğ‘–ğ‘šğ‘ doesnâ€™t match it yet. We need to wrap m b in
-- IdentityT to make it typecheck:

-- Remember:
-- ? IdentityT :: f a -> IdentityT f a

{-
instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f =
        let aimb = join (fmap runIdentityT (fmap f ma))
         in IdentityT aimb
-}

-- This should compile. We rewrap m b back in the IdentityT type and
-- we should be good to go.

-- ? Refactoring

-- Now that we have something that works, letâ€™s refactor. Weâ€™d like to
-- improve our implementation of (>>=). Taking things one step at
-- a time is usually more successful than trying to rewrite all at once,
-- especially once you have a baseline version that you know should
-- work. How should we improve this line?

-- * IdentityT $ join (fmap runIdentityT (fmap f ma))

-- Well, one of the Functor laws tells us something about fmapping twice:

-- Functor law:

-- * fmap (f . g) == fmap f . fmap g

-- Indeed! So we can change that line to the following and it should be
-- identical:

-- * IdentityT $ join (fmap (runIdentityT . f) ma)

-- Now it seems suspicious that weâ€™re fmapping and also using join on
-- the result of having fmapped the two functions we composed. Isnâ€™t
-- join composed with fmap just (>>=)?

-- * x >>= f = join (fmap f x)

-- Accordingly, we can change our Monad instance to the following:

instance (Monad m) => Monad (IdentityT m) where
    (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
    (IdentityT ma) >>= f = IdentityT $ ma >>= runIdentityT . f

-- And that should work still! We have a type constructor now (IdentityT)
-- that takes a monad as an argument and returns a monad as a result.

-- This implementation can be written other ways. In the transformers
-- library, for example, itâ€™s written like this:

-- * m >>= k = IdentityT $ runIdentityT . k =<< runIdentityT m

-- Take a moment and work out for yourself how that is functionally
-- equivalent to our implementation.

-- 920
-- ? The essential extra of Monad transformers

-- It may not seem like it, but the IdentityT monad transformer actually
-- captures the essence of transformers generally

--  We only embarked on this quest because we couldnâ€™t be guaranteed
--  a Monad instance given the composition of two types

-- Given that, we know having Functor/Applicative/Monad at our disposal isnâ€™t
-- enough to make that new Monad instance.

-- ? So what was novel in the following code?
-- (>>=) :: IdentityT m a -> (a -> IdentityT m b) -> IdentityT m b
-- (IdentityT ma) >>= f = IdentityT $ ma >>= runIdentityT . f

-- Well, it wasnâ€™t the pattern match on IdentityT; we get that from the
-- Functor anyway:
-- Not this
-- (IdentityT ma) ...

-- It wasnâ€™t the ability to (>>=) functions over the ma value of type ğ‘šğ‘,
-- we get that from the Monad constraint on ğ‘š anyway.

-- Not this
-- ... ma >>= ...

-- We needed to know one of the types concretely so that we could use
-- runIdentityT (essentially fmapping a fold of the IdentityT structure)
-- and then repack the value in IdentityT

-- We needed to know IdentityT
-- concretely to be able to do this
-- IdentityT .. runIdentityT ...

{-
   As youâ€™ll recall, until we used runIdentityT we couldnâ€™t get the types
   to fit because IdentityT was wedged in the middle of two bits of ğ‘š. It
   * turns out to be impossible to fix that using only Functor, Applicative,
   * and Monad
-}

-- This is an example of why we canâ€™t just make a Monad
-- instance for the Compose type, but we can make a transformer type
-- like IdentityT where we leverage information specific to the type
-- and combine it with any other type that has a Monad instance.

-- ? In general, in order to make the types fit, weâ€™ll need some way to fold
-- ? and reconstruct the type we have concrete information for.
